# Disable terminal freezing with ctrl+s
stty stop undef

# 0.1 second delay after triggering vi mode instead of default 0.4
export KEYTIMEOUT=1

# Enable bash-style comments
setopt interactivecomments
export KSH_ARRAYS=1

# History
setopt sharehistory
HISTSIZE=1000
SAVEHIST=10000
HISTFILE=~/.zsh_history

# vi mode
bindkey -v

# Change cursor shape for different vi modes
function zle-keymap-select {
  if [[ ${KEYMAP} == vicmd ]] ||
     [[ $1 = 'block' ]]; then
    echo -ne '\e[1 q'
  elif [[ ${KEYMAP} == main ]] ||
       [[ ${KEYMAP} == viins ]] ||
       [[ ${KEYMAP} = '' ]] ||
       [[ $1 = 'beam' ]]; then
    echo -ne '\e[5 q'
  fi
}
zle -N zle-keymap-select
zle-line-init() {
    zle -K viins
    echo -ne "\e[5 q"
}
zle -N zle-line-init
echo -ne '\e[5 q'
preexec() { echo -ne '\e[5 q' ;}

# Autocompletion
autoload -Uz compinit
zstyle ':completion:*' menu select
zstyle ':completion:*' matcher-list '' \
    'm:{a-zA-Z}={A-Za-z}' 'r:|=*' 'l:|=* r:|=*' # Case-insensitive
zmodload zsh/complist
compinit
_comp_options+=(globdots) # Include hidden files

# pure prompt
fpath+=($HOME/.config/zsh/pure)
autoload -U promptinit; promptinit
prompt pure
PURE_PROMPT_SYMBOL='$'
PURE_PROMPT_VICMD_SYMBOL='%%'
PURE_SUSPENDED_JOBS_SYMBOL='&'
zstyle :prompt:pure:prompt:success color '#FFAA00'
zstyle :prompt:pure:prompt:error color '#A13D2D'
zstyle :prompt:pure:suspended_jobs color '#808080'

# Reverse search with ctrl+r
bindkey '^R' history-incremental-search-backward

# Edit line in $EDITOR with ctrl+e
autoload edit-command-line; zle -N edit-command-line
bindkey '^e' edit-command-line

# shift+hjkl navigates autocompletion menu (after pressing tab)
bindkey -M menuselect "\H" vi-backward-char
bindkey -M menuselect "\K" vi-up-line-or-history
bindkey -M menuselect "\L" vi-forward-char
bindkey -M menuselect "\J" vi-down-line-or-history
bindkey -v '^?' backward-delete-char

# bind ctrl + arrow right/left arrow keys to forward word / backward word
bindkey "^[[1;5C" forward-word
bindkey "^[[1;5D" backward-word

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
[ -f "$HOME/.config/localshrc" ] && source "$HOME/.config/localshrc"
[ -f "$HOME/.config/aliasrc" ] && source "$HOME/.config/aliasrc"
[ -f "$HOME/.config/lessrc" ] && source "$HOME/.config/lessrc"
[ -f "$HOME/.profile" ] && source "$HOME/.profile"

bindkey -s '^k' 'exit\n'
bindkey -s '^o' 'lfcd\n'
bindkey -s '^p' 'lfip\n'

# enable syntax highlighting
hl() { source /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh; }

# zoxide setup
if command -v zoxide &> /dev/null; then eval "$(zoxide init zsh)"; fi
